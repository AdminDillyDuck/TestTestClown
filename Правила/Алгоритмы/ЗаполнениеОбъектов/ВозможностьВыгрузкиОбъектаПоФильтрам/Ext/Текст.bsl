МетаданныеОбъекта = ПараметрИсточник.Метаданные();

//фильтр по дате выгрузки

Если ПараметрИсточник.Дата < Параметры.ДатаНачалаВыгрузкиДокументов Тогда
	
	Отказ = Истина;
	
КонецЕсли;  

//фильтр по организациям

Если (НЕ Отказ)
	И Параметры.ИспользоватьФильтрПоОрганизациям
	И ОбщегоНазначения.ЕстьРеквизитДокумента("Организация", МетаданныеОбъекта) 
	И Параметры.МассивОрганизаций.Найти(ПараметрИсточник.Организация) = Неопределено Тогда
	
	Отказ = Истина;	
	
КонецЕсли;

//фильтр по подразделениям

Если  (НЕ Отказ)
	И Параметры.ИспользоватьФильтрПоПодразделениям
	И ОбщегоНазначения.ЕстьРеквизитДокумента("Подразделение", МетаданныеОбъекта) 
	И Параметры.МассивПодразделений.Найти(ПараметрИсточник.Подразделение) = Неопределено Тогда
	
	Отказ = Истина;	
	
КонецЕсли;

//фильтр по складам

Если  (НЕ Отказ)
	И Параметры.ИспользоватьФильтрПоСкладам Тогда
	
	
	Если МетаданныеОбъекта = Метаданные.Документы.АвансовыйОтчет
		ИЛИ МетаданныеОбъекта = Метаданные.Документы.ВозвратТоваровОтПокупателя
		ИЛИ МетаданныеОбъекта = Метаданные.Документы.ПоступлениеТоваровУслуг  Тогда
		
		ИмяРеквизитаСклад = "СкладОрдер";
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Документы.ЗаказПокупателя Тогда
		
		ИмяРеквизитаСклад = "СкладГруппа";
		
		
	ИначеЕсли МетаданныеОбъекта.Реквизиты.Найти("Склад") <> Неопределено Тогда 		
		
		ИмяРеквизитаСклад = "Склад";
		
	Иначе 
		
		ИмяРеквизитаСклад = "";
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ИмяРеквизитаСклад) Тогда
		
		Если Параметры.МассивСкладов.Найти(ПараметрИсточник[ИмяРеквизитаСклад]) = Неопределено Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Документы.ПеремещениеТоваров 
		И Параметры.МассивСкладов.Найти(ПараметрИсточник.СкладОтправитель) = Неопределено 
		И Параметры.МассивСкладов.Найти(ПараметрИсточник.СкладПолучатель) = Неопределено Тогда
		
		Отказ = Истина;
		
	КонецЕсли;	
	
КонецЕсли;

//фильтр по БУ

Если (НЕ Отказ)
	И Параметры.ПередаватьТолькоДокументыОтраженныеВБухгалтерскомУчете 
	И ОбщегоНазначения.ЕстьРеквизитДокумента("ОтражатьВБухгалтерскомУчете", МетаданныеОбъекта) 
	И НЕ ПараметрИсточник.ОтражатьВБухгалтерскомУчете Тогда
	
	Отказ = Истина;
	
КонецЕсли;