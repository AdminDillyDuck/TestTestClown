Запрос = Новый Запрос;
Запрос.Текст =
"ВЫБРАТЬ
|	ПоступлениеТоваровУслугТовары.Номенклатура КАК Номенклатура,
|	СУММА(ПоступлениеТоваровУслугТовары.Количество) КАК Количество,
|	ПоступлениеТоваровУслугТовары.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
|	ПоступлениеТоваровУслугТовары.Ссылка КАК Ссылка
|ПОМЕСТИТЬ ВТ_ТаблицаТовары
|ИЗ
|	Документ.ПоступлениеТоваровУслуг.Товары КАК ПоступлениеТоваровУслугТовары
|ГДЕ
|	ПоступлениеТоваровУслугТовары.Ссылка = &Ссылка
|
|СГРУППИРОВАТЬ ПО
|	ПоступлениеТоваровУслугТовары.Номенклатура,
|	ПоступлениеТоваровУслугТовары.ХарактеристикаНоменклатуры,
|	ПоступлениеТоваровУслугТовары.Ссылка
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ПоступлениеТоваровУслугСписаниеМатериалов.Номенклатура КАК Номенклатура,
|	ПоступлениеТоваровУслугСписаниеМатериалов.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
|	СУММА(ПоступлениеТоваровУслугСписаниеМатериалов.Количество) КАК Количество,
|	ПоступлениеТоваровУслугСписаниеМатериалов.Ссылка КАК Ссылка,
|	ПоступлениеТоваровУслугСписаниеМатериалов.Продукция КАК Продукция,
|	ПоступлениеТоваровУслугСписаниеМатериалов.ХарактеристикаПродукции КАК ХарактеристикаПродукции
|ПОМЕСТИТЬ ВТ_СписаниеМатериалов
|ИЗ
|	Документ.ПоступлениеТоваровУслуг.СписаниеМатериалов КАК ПоступлениеТоваровУслугСписаниеМатериалов
|ГДЕ
|	ПоступлениеТоваровУслугСписаниеМатериалов.Ссылка = &Ссылка
|
|СГРУППИРОВАТЬ ПО
|	ПоступлениеТоваровУслугСписаниеМатериалов.Номенклатура,
|	ПоступлениеТоваровУслугСписаниеМатериалов.ХарактеристикаНоменклатуры,
|	ПоступлениеТоваровУслугСписаниеМатериалов.Ссылка,
|	ПоступлениеТоваровУслугСписаниеМатериалов.Продукция,
|	ПоступлениеТоваровУслугСписаниеМатериалов.ХарактеристикаПродукции
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ПоступлениеТоваровУслугУслуги.Номенклатура КАК Номенклатура,
|	ПоступлениеТоваровУслугУслуги.ХарактеристикаНоменклатуры КАК ХарактеристикаНоменклатуры,
|	ПоступлениеТоваровУслугУслуги.Ссылка КАК Ссылка,
|	СУММА(ПоступлениеТоваровУслугУслуги.Сумма) КАК Сумма,
|	СУММА(ПоступлениеТоваровУслугУслуги.СуммаНДС) КАК СуммаНДС,
|	ПоступлениеТоваровУслугУслуги.Продукция КАК Продукция,
|	ПоступлениеТоваровУслугУслуги.ХарактеристикаПродукции КАК ХарактеристикаПродукции
|ПОМЕСТИТЬ ВТ_Услуги
|ИЗ
|	Документ.ПоступлениеТоваровУслуг.Услуги КАК ПоступлениеТоваровУслугУслуги
|ГДЕ
|	ПоступлениеТоваровУслугУслуги.Ссылка = &Ссылка
|
|СГРУППИРОВАТЬ ПО
|	ПоступлениеТоваровУслугУслуги.Номенклатура,
|	ПоступлениеТоваровУслугУслуги.Ссылка,
|	ПоступлениеТоваровУслугУслуги.ХарактеристикаНоменклатуры,
|	ПоступлениеТоваровУслугУслуги.Продукция,
|	ПоступлениеТоваровУслугУслуги.ХарактеристикаПродукции
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВТ_ТаблицаТовары.Номенклатура КАК Номенклатура,
|	СУММА(ВТ_ТаблицаТовары.Количество) КАК Количество,
|	ВТ_СписаниеМатериалов.Номенклатура КАК Комплектующая,
|	ВТ_СписаниеМатериалов.ХарактеристикаНоменклатуры КАК ХарактеристикаКомплектующей,
|	ВТ_ТаблицаТовары.Ссылка КАК Ссылка,
|	СУММА(ЕСТЬNULL(ВТ_Услуги.Сумма, 0) - ЕСТЬNULL(ВТ_Услуги.СуммаНДС, 0)) КАК СуммаУслуг
|ПОМЕСТИТЬ ВТ_Товары
|ИЗ
|	ВТ_ТаблицаТовары КАК ВТ_ТаблицаТовары
|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Услуги КАК ВТ_Услуги
|		ПО ВТ_ТаблицаТовары.Номенклатура = ВТ_Услуги.Продукция
|			И ВТ_ТаблицаТовары.ХарактеристикаНоменклатуры = ВТ_Услуги.ХарактеристикаПродукции
|			И ВТ_ТаблицаТовары.Ссылка = ВТ_Услуги.Ссылка
|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СписаниеМатериалов КАК ВТ_СписаниеМатериалов
|		ПО ВТ_ТаблицаТовары.Номенклатура = ВТ_СписаниеМатериалов.Продукция
|			И ВТ_ТаблицаТовары.ХарактеристикаНоменклатуры = ВТ_СписаниеМатериалов.ХарактеристикаПродукции
|			И ВТ_ТаблицаТовары.Ссылка = ВТ_СписаниеМатериалов.Ссылка
|
|СГРУППИРОВАТЬ ПО
|	ВТ_ТаблицаТовары.Номенклатура,
|	ВТ_СписаниеМатериалов.Номенклатура,
|	ВТ_СписаниеМатериалов.ХарактеристикаНоменклатуры,
|	ВТ_ТаблицаТовары.Ссылка
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВТ_Товары.Номенклатура КАК Номенклатура,
|	МАКСИМУМ(ВТ_Товары.Количество) КАК Количество,
|	СУММА(ЕСТЬNULL(ПартииТоваровНаСкладахОбороты.СтоимостьРасход, 0)) КАК СуммаСписания,
|	ВТ_Товары.СуммаУслуг КАК СуммаУслуг
|ПОМЕСТИТЬ ВТ_Группировка
|ИЗ
|	ВТ_Товары КАК ВТ_Товары
|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПартииТоваровНаСкладах.Обороты(
|				&Дата,
|				&Дата,
|				Регистратор,
|				(Номенклатура, ХарактеристикаНоменклатуры) В
|					(ВЫБРАТЬ
|						ВТ_Товары.Комплектующая КАК Комплектующая,
|						ВТ_Товары.ХарактеристикаКомплектующей КАК ХарактеристикаКомплектующей
|					ИЗ
|						ВТ_Товары КАК ВТ_Товары)) КАК ПартииТоваровНаСкладахОбороты
|		ПО ВТ_Товары.Комплектующая = ПартииТоваровНаСкладахОбороты.Номенклатура
|			И ВТ_Товары.ХарактеристикаКомплектующей = ПартииТоваровНаСкладахОбороты.ХарактеристикаНоменклатуры
|			И ВТ_Товары.Ссылка = ПартииТоваровНаСкладахОбороты.Регистратор
|
|СГРУППИРОВАТЬ ПО
|	ВТ_Товары.Номенклатура,
|	ВТ_Товары.СуммаУслуг
|;
|
|////////////////////////////////////////////////////////////////////////////////
|ВЫБРАТЬ
|	ВТ_Группировка.Номенклатура КАК Номенклатура,
|	ВТ_Группировка.Количество КАК Количество,
|	ВЫБОР
|		КОГДА ВТ_Группировка.Количество = 0
|			ТОГДА 0
|		ИНАЧЕ (ЕСТЬNULL(ВТ_Группировка.СуммаСписания, 0) + ВТ_Группировка.СуммаУслуг) / ВТ_Группировка.Количество
|	КОНЕЦ КАК Цена,
|	ЕСТЬNULL(ВТ_Группировка.СуммаСписания, 0) + ВТ_Группировка.СуммаУслуг КАК Сумма
|ИЗ
|	ВТ_Группировка КАК ВТ_Группировка";

Запрос.УстановитьПараметр("Дата", Источник.Дата);
Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);

КоллекцияОбъектов = Запрос.Выполнить().Выгрузить();

// 1СTeam Фасхетдинов Д.М. 14.02.2023 [T1C-2826] НАЧАЛО
Если КоллекцияОбъектов.Количество() Тогда
	КоллекцияОбъектов.Колонки.Добавить("Счет");             
	МассивНоменклатур = БП_ОбщийПривилегированный.ПолучитьЗначениеНастройки("ДавальческоеСырье","НоменклатураДляВыгрузкиСоСчетом10051",Истина);
	Если МассивНоменклатур.Количество() Тогда
		Для Каждого СтрокаКоллеции Из КоллекцияОбъектов Цикл
			СтрокаКоллеции.Счет = ?(МассивНоменклатур.Найти(СтрокаКоллеции.Номенклатура) <> Неопределено,"10.05.1","43");
		КонецЦикла;	
	КонецЕсли;		
КонецЕсли;
// 1СTeam Фасхетдинов Д.М. 14.02.2023 [T1C-2826] КОНЕЦ