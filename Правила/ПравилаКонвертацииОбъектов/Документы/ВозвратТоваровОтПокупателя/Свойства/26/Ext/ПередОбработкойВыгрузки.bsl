
ТаблицаПоТоварам = Источник.Товары.Выгрузить();
Запросы.Комплекты.УстановитьПараметр("МассивНоменклатуры", ТаблицаПоТоварам.ВыгрузитьКолонку("Номенклатура"));
ТабКомплекты = Запросы.Комплекты.Выполнить().Выгрузить();
ТаблицаПоТоварам.Колонки.Добавить("Комплект");
ТаблицаПоТоварам.Колонки.Удалить("Коэффициент");
Для каждого СтрокаТовары Из ТаблицаПоТоварам Цикл
	НайденнаяСтрока = ТабКомплекты.Найти(СтрокаТовары.Номенклатура, "Ссылка");
	Если НайденнаяСтрока <> Неопределено Тогда
		СтрокаТовары.Комплект = НайденнаяСтрока.Комплект;
	КонецЕсли;
КонецЦикла;
ТабТовары = УправлениеЗапасами.СформироватьТаблицуКомплектующих(ТаблицаПоТоварам, Источник);
ТабТовары.Колонки.Добавить("Коэффициент");
Для каждого СтрокаТовары Из ТабТовары Цикл
	СтрокаТовары.Коэффициент = ?(СтрокаТовары.ЕдиницаИзмерения.Коэффициент <> 0, СтрокаТовары.ЕдиницаИзмерения.Коэффициент, 1);
	СтрокаТовары.Количество = СтрокаТовары.Количество * СтрокаТовары.Коэффициент;
	СтрокаТовары.Цена = ?(СтрокаТовары.Количество <> 0, СтрокаТовары.Сумма / СтрокаТовары.Количество, 0);
КонецЦикла;

Запросы.СписанныеПартииВозвратТоваровОтПокупателя.УстановитьПараметр("ДокументСсылка", Источник.Ссылка);
Запросы.СписанныеПартииВозвратТоваровОтПокупателя.УстановитьПараметр("Расход", ВидДвиженияНакопления.Приход);
РезультатЗапроса = Запросы.СписанныеПартииВозвратТоваровОтПокупателя.Выполнить();

ТабПартии = РезультатЗапроса.Выгрузить();

КоллекцияОбъектов = ТабТовары.Скопировать();
КоллекцияОбъектов.Очистить();
//КоллекцияОбъектов.Колонки.Добавить("Комиссия");


//Если НЕ УзелДляОбмена.ВариантПереносаСебестоимостиСписанияВБухгалтериюПредприятия = Перечисления.ВариантыПереносаСебестоимостиСписанияВБухгалтериюПредприятия.ПереноситьДокументПартииИСебестоимость Тогда
//		КоллекцияОбъектов.Колонки.Удалить("Себестоимость");
//КонецЕсли;

// 1СTeam Томилин А.П. 25.10.2022 [T1C-2645] НАЧАЛО
Если Параметры.Свойство("КодУзлаОтладки") И ЗначениеЗаполнено(Параметры.КодУзлаОтладки) Тогда
	УзелДляОбмена = ПланыОбмена.ОбменУправлениеТорговлей103БухгалтерияПредприятия30.НайтиПоКоду(Параметры.КодУзлаОтладки);
КонецЕсли;
// 1СTeam Томилин А.П. 25.10.2022 [T1C-2645] КОНЕЦ

СтрокаКолонок = "";
Для каждого Колонка Из КоллекцияОбъектов.Колонки Цикл
	Если Колонка.Имя = "Количество" 
	 ИЛИ Колонка.Имя = "Сумма" 
	 ИЛИ Колонка.Имя = "СуммаНДС" 
	 ИЛИ Колонка.Имя = "Себестоимость" Тогда
		Продолжить;
	КонецЕсли;
	СтрокаКолонок = СтрокаКолонок + ", " + Колонка.Имя;
КонецЦикла;
СтрокаКолонок = Сред(СтрокаКолонок, 2);
	

Для каждого СтрокаТЧ ИЗ ТабТовары Цикл
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура", СтрокаТЧ.Номенклатура);
	СтруктураПоиска.Вставить("ХарактеристикаНоменклатуры", СтрокаТЧ.ХарактеристикаНоменклатуры);
	Если СтрокаТЧ.Номенклатура.ВестиПартионныйУчетПоСериям Тогда
		СтруктураПоиска.Вставить("СерияНоменклатуры", СтрокаТЧ.СерияНоменклатуры);
	КонецЕсли;
	
	НайденныеСтроки = ТабПартии.НайтиСтроки(СтруктураПоиска);
	
	Цена = ?(СтрокаТЧ.Количество <> 0, СтрокаТЧ.Сумма / СтрокаТЧ.Количество, 0);
	
	ОстКоличество = СтрокаТЧ.Количество;
	ОстСумма      = СтрокаТЧ.Сумма;
	ОстСуммаНДС   = СтрокаТЧ.СуммаНДС;
	Для каждого СтрокаПартии ИЗ НайденныеСтроки Цикл
		Если ОстКоличество > 0 И СтрокаПартии.Количество > 0 Тогда
			НоваяСтрока = КоллекцияОбъектов.Добавить();
			Для каждого Колонка ИЗ ТабТовары.Колонки Цикл
				Если Колонка.Имя = "НомерСтроки" 
				 ИЛИ Колонка.Имя = "Сумма" 
				 ИЛИ Колонка.Имя = "СуммаНДС" Тогда
				 //ИЛИ Колонка.Имя = "Себестоимость" Тогда

					Продолжить;
				КонецЕсли;
				НоваяСтрока[Колонка.Имя] = СтрокаТЧ[Колонка.Имя];
			КонецЦикла;
			
			НоваяСтрока.Количество = Мин(СтрокаПартии.Количество, ОстКоличество);
			
			НоваяСтрока.Цена	   = Цена;
			
			Если СтрокаПартии.СтатусПартии = Перечисления.СтатусыПартийТоваров.НаКомиссию Тогда
				НоваяСтрока.Себестоимость = 0; 
			Иначе	
				НоваяСтрока.Себестоимость = (СтрокаПартии.Стоимость*НоваяСтрока.Количество)/СтрокаПартии.Количество;
			КонецЕсли;		

			Если НоваяСтрока.Количество = ОстКоличество Тогда
				НоваяСтрока.Сумма    = ОстСумма;
				НоваяСтрока.СуммаНДС = ОстСуммаНДС;
			Иначе
				НоваяСтрока.Сумма      = НоваяСтрока.Цена * НоваяСтрока.Количество;
				НоваяСтрока.СуммаНДС   = Ценообразование.РассчитатьСуммуНДС(НоваяСтрока.Сумма, Источник.УчитыватьНДС, Источник.СуммаВключаетНДС,
	    	                                               Ценообразование.ПолучитьСтавкуНДС(НоваяСтрока.СтавкаНДС));
			КонецЕсли;
													   
			// Уменьшим количество и стоимость в таблице партий, чтобы не использовать одну и ту же партию несколько раз
			СтрокаПартии.Количество = СтрокаПартии.Количество - НоваяСтрока.Количество;
			Если УзелДляОбмена.ВариантПереносаСебестоимостиСписанияВБухгалтериюПредприятия = Перечисления.ВариантыПереносаСебестоимостиСписанияВБухгалтериюПредприятия.ПереноситьДокументПартииИСебестоимость Тогда
				СтрокаПартии.Стоимость  = СтрокаПартии.Стоимость  - НоваяСтрока.Себестоимость;
			КонецЕсли;

			ОстКоличество = ОстКоличество - НоваяСтрока.Количество;
			ОстСумма      = ОстСумма      - НоваяСтрока.Сумма;
			ОстСуммаНДС   = ОстСуммаНДС   - НоваяСтрока.СуммаНДС;
			
		КонецЕсли;
	КонецЦикла;
	
	// Если пратий нет все равно выгружаем остаток товаров как собственные
	Если ОстКоличество > 0 Тогда
		НоваяСтрока = КоллекцияОбъектов.Добавить();
		Для каждого Колонка ИЗ ТабТовары.Колонки Цикл
			НоваяСтрока[Колонка.Имя] = СтрокаТЧ[Колонка.Имя];
		КонецЦикла;
		НоваяСтрока.Количество = ОстКоличество;
		//НоваяСтрока.Комиссия   = Ложь;
		НоваяСтрока.Сумма      = ОстСумма;
		НоваяСтрока.СуммаНДС   = ОстСуммаНДС;
	КонецЕсли;
	
КонецЦикла;

КоллекцияОбъектов.Свернуть(СтрокаКолонок, "Количество, Сумма, СуммаНДС"
+ ?(КоллекцияОбъектов.Колонки.Найти("Себестоимость") <> Неопределено,", Себестоимость",""));